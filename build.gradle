apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

project.version = '1.9.12'
project.group = 'org.jastadd'
project.archivesBaseName = 'jastaddgradle'

// Ensure the plugin runs with Java 7.
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
	compile gradleApi()
	compile localGroovy()
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories.mavenDeployer {
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

		repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			if (project.hasProperty('ossrhUsername')) {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}

		snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
			if (project.hasProperty('ossrhUsername')) {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}

		pom.project {
			name 'JastAddGradle'
			packaging 'jar'
			description 'A Gradle build plugin for building JastAdd projects.'
			url 'http://jastadd.org'
			licenses {
				license {
					name 'Modified BSD License'
					url 'http://opensource.org/licenses/BSD-3-Clause'
					distribution 'repo'
				}
			}
			developers {
				developer {
					name 'Jesper Ã–qvist'
					email 'jesper.oqvist@cs.lth.se'
				}
			}
			scm {
				connection 'scm:git:https://bitbucket.org/joqvist/jastaddgradle.git'
				url 'https://bitbucket.org/joqvist/jastaddgradle'
			}
		}
	}
}
